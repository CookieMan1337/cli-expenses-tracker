plugins {
    id 'java'
    id 'application'
}

group = 'com.ledgerlite'
version = '1.0.0'

application {
    mainClass.set("com.ledgerlite.app.LedgerLiteApp")
}
repositories {
    mavenCentral()
}

dependencies {
    implementation("com.fasterxml.jackson.core:jackson-databind:2.15.2")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2")

    implementation("org.slf4j:slf4j-api:2.0.9")
    implementation("ch.qos.logback:logback-classic:1.4.11")

    testImplementation(platform("org.junit:junit-bom:5.10.0"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.mockito:mockito-core:5.7.0")
    testImplementation("org.assertj:assertj-core:3.24.2")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': application.mainClass,
                'Implementation-Version': archiveVersion
        )
    }
}

tasks.register('fatJar', Jar) {
    group = 'build'
    description = 'Create a fat JAR with all dependencies'
    archiveClassifier = 'all'

    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }
                .collect { zipTree(it) }
    }

    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }

    }
}
// Настройка стандартной задачи run для работы с System.in
run {
    standardInput = System.in
    systemProperty "sun.stdout.encoding", "UTF-8"
    systemProperty "sun.err.encoding", "UTF-8"
}


tasks.withType(JavaExec) {
    systemProperty "file.encoding", "UTF-8"
    systemProperty "sun.stdout.encoding", "UTF-8"
    systemProperty "sun.err.encoding", "UTF-8"
}

